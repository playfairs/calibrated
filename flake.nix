{
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    treefmt-nix.url = "github:numtide/treefmt-nix";
    rust-overlay.url = "github:oxalica/rust-overlay";
    naersk.url = "github:nix-community/naersk";
  };
  outputs =
    {
      nixpkgs,
      naersk,
      flake-utils,
      treefmt-nix,
      rust-overlay,
      ...
    }:
    let
      overlays = {
        default = final: prev: {
          bytelab = (final.callPackage naersk { }).buildPackage {
            pname = "bytelabs";
            src = ./.;
          };
        };
      };
    in
    flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = import nixpkgs {
          inherit system;
          overlays = [
            (import rust-overlay)
            overlays.default
          ];
        };
      in
      {
        devShells.default = pkgs.mkShell rec {
          buildInputs =
            with pkgs;
            [
              nil
              atk
              nixd
              typos
              tokei
              swift
              helix
              clippy
              librsvg
              rustfmt
              libsoup_3
              xdg-utils
              pkg-config
              cargo-bundle
              rust-analyzer
              rust-bin.nightly.latest.default
            ]
            ++ lib.optionals stdenv.isLinux [
              alsa-lib
              jack2
            ];

          runtimeLibs =
            with pkgs;
            lib.optionals stdenv.isLinux [
              expat
              fontconfig
              freetype
              freetype.dev
              vulkan-loader
              libGL
              pkg-config
              xorg.libX11
              xorg.libXcursor
              xorg.libXi
              xorg.libXrandr
              wayland
              libxkbcommon
            ];

          LD_LIBRARY_PATH = pkgs.lib.makeLibraryPath runtimeLibs;
        };

        packages.default = pkgs.bytelab;
        formatter =
          (treefmt-nix.lib.evalModule pkgs (_: {
            projectRootFile = ".git/config";
            programs = {
              nixfmt.enable = true;
              nixf-diagnose.enable = true;
              rustfmt.enable = true;
              toml-sort.enable = true;
            };
            settings.formatter.rustfmt = {
              unstable-features = true;
              tab_spaces = 2;
              trailing_semicolon = false;
              style_edition = "2024";
              use_try_shorthand = true;
              wrap_comments = true;
            };
          })).config.build.wrapper;
      }
    )
    // {
      inherit overlays;
    };
}
